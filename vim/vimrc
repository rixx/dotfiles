"""""""""""""""""""""""""""""""""""""""""""""
""  General Settings                       ""
"""""""""""""""""""""""""""""""""""""""""""""

"" Add .config/dotfiles/vim to runtimepath. Necessary for colorscheme
let &rtp .= ','.expand('~/.config/dotfiles/vim/')

"" Base settings
set autoread          " Read file again if it changes while open and unmodified in vim
set autowrite         " Write to disk when navigating files
set hidden            " Allow unsaved background buffers and remember marks/undo for them
set formatoptions+=j  " Delete comment character when joining comment lines
set diffopt=vertical  " Make sure diffs get shown side-by-side
set ignorecase        " Ignore case when searching
set smartcase         " Do not ignore case when searching for upper-case
set showmatch         " When typing a bracket, highlight match if visible
set matchtime=2       " Only highlight the matching bracket for .2s
set splitbelow        " Split below by default
set splitright        " Split right by default
set report=0          " Always report changed lines
set lazyredraw        " Faster, lazy redrawing when executing macros

"" Text processing and indentation
set smartindent       " Smart autoindenting when starting a new line
set expandtab         " Insert spaces when using tab key and indent shortcuts
set shiftwidth=4      " Use tabstop value for auto indent
set softtabstop=-1    " Use tab to go to next soft tabstop

"" Visual settings
set background=light  " Boo to edgy dark themes
colorscheme duochrome " Plugin setting
syntax enable         " Always highlight
set synmaxcol=400     " But only highlight the first 400 columns
highlight MatchParen cterm=bold ctermbg=cyan ctermfg=white  " Matching parens
match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$' " Highlight VCS conflict markers
set relativenumber    " Show relative line numbers
set number            " But also show current number
set foldcolumn=3      " Fixed side column width
set nofoldenable      " Imagine knowing how to use folds
set foldmethod=marker " But if folds are used, use markers
set cursorline        " Highlight current line
set scrolloff=999     " Keep cursor centered
set sidescrolloff=5   " Keep cursor 5 columns from screen edge
set mouse=a           " Mouse integration in all modes
set mousehide         " Hide mouse while typing
set list              " Display whitespace
set listchars=tab:┄┄╴,trail:░,extends:❯,precedes:❮,nbsp:·

"" Completion
set wildmode=list:longest,full  " List matches and tab-cycle through them
set wildignore=*.o,*~,*.pyc,.tox,*.egg-info,.git,.hg,.svn,*.bmp,*.gif,*.jpg,*.jpeg,*.png,*.DS_Store
set completeopt=noinsert,menuone,noselect,preview
set shortmess+=c        " suppress the annoying 'match x of y', 'The only match' and 'Pattern not found' messages

"" Other settings
" Show title, set to the default without the trailing "- Nvim"
set title
set titlestring=%t%(\ %M%)%(\ \(%{expand(\"%:~:h\")}\)%)%a

"" Set history size to max and persist undo
set history=10000
set undolevels=10000
set undodir=/home/rixx/.cache/vim/undo
set undofile
set backupdir=/home/rixx/.cache/vim/backup

" Remember and restore cursor position
autocmd BufReadPost *
     \ if line("'\"") > 3 && line("'\"") <= line("$") |
     \   exe "normal! g`\"" |
     \ endif

" Except for commit messages; start on top here every time
autocmd BufReadPost COMMIT_EDITMSG
  \ exe "normal! gg"

" And in new mails, jump below the header block
autocmd FileType mail execute "normal /^\\n\<CR>o"


" Anything that is put in a register (mostly by yanking or deleting) is also put in the
" special + register, which is synced with the system clipboard
set clipboard+=unnamedplus

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
""  Key mappings                                          ""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"" set the map leader to ' '
let mapleader = "\<Space>"
let g:mapleader = "\<Space>"

"" Leader shortcuts
noremap <leader>ss :setlocal spell!<cr>
nnoremap zz :w\|bd<cr>
" Close all other splits
nnoremap <leader>o :only<cr>

" Easy buffer navigation
noremap <C-h>  <C-w>h
noremap <C-j>  <C-w>j
noremap <C-k>  <C-w>k
noremap <C-l>  <C-w>l
noremap <leader>v <C-w>v
noremap <leader>w <C-w>w

" remap F6 to breakpoint
nmap <F6> obreakpoint()<C-c>

" gx, but open paths with xdg-open async
nnoremap gX :silent :execute "!xdg-open" expand('%:p:h') . "/" . expand("<cfile>") " &"<cr>

" Exit terminal input mode with Escape
tnoremap <ESC><ESC> <C-\><C-N>

" really really save
cmap w!! w !sudo tee %
map q: :q

" stay in visual after indent
vnoremap < <gv
vnoremap > >gv

" <Ctrl-l> redraws the screen and removes any search highlighting.
nnoremap <silent> <C-o> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>
nnoremap , :

" Repeat last macro
nnoremap Q @@

" Remove trailing whitespace
nnoremap <leader>W :%s/\s\+$//<cr>:let @/=''<CR>

" Grab full document
nnoremap <leader>a ggVG

" Format JSON
nmap <leader>J :%!python -m json.tool<CR>:%s/\s\+$//g<CR>

nmap <leader>1 <Plug>AirlineSelectTab1
nmap <leader>2 <Plug>AirlineSelectTab2
nmap <leader>3 <Plug>AirlineSelectTab3
nmap <leader>4 <Plug>AirlineSelectTab4
nmap <leader>5 <Plug>AirlineSelectTab5
nmap <leader>6 <Plug>AirlineSelectTab6
nmap <leader>7 <Plug>AirlineSelectTab7
nmap <leader>8 <Plug>AirlineSelectTab8
nmap <leader>9 <Plug>AirlineSelectTab9
nmap <leader>0 <Plug>AirlineSelectTab0
" nmap <leader>N <Plug>AirlineSelectPrevTab
" nmap <leader>n <Plug>AirlineSelectNextTab
nmap <leader>N :bp<cr>
nmap <leader>n :bn<cr>
nmap <leader>d :bd<cr>


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
""  File type specific settings                           ""
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" assume .md files are markdown, not modula-2, lol
augroup markdown
  autocmd!
  autocmd BufNewFile,BufReadPost *.md set filetype=markdown
  autocmd BufNewFile,BufReadPost *.md set colorcolumn=120
  autocmd BufNewFile,BufReadPost *.md set tw=120
  autocmd BufNewFile,BufReadPost *.md set formatoptions+=t
  autocmd BufNewFile,BufReadPost Dockerfile set filetype=dockerfile
  autocmd BufNewFile,BufReadPost *.vue set filetype=vue
  autocmd BufNewFile,BufRead /tmp/*mutt* set noautoindent filetype=mail wm=0 tw=78 nonumber digraph nolist nopaste
  " Conceal links in Markdown
  autocmd Filetype markdown syn region markdownLink matchgroup=markdownLinkDelimiter start="(" end=")" contains=markdownUrl keepend contained conceal
  autocmd Filetype markdown syn region markdownLinkText matchgroup=markdownLinkTextDelimiter start="!\=\[\%(\%(\_[^][]\|\[\_[^][]*\]\)*]\%( \=[[(]\)\)\@=" end="\]\%( \=[[(]\)\@=" nextgroup=markdownLink,markdownId skipwhite contains=@markdownInline,markdownLineStart concealends
  autocmd BufNewFile,BufReadPost *.md set conceallevel=2
augroup end

augroup development
  autocmd!
  " Compute syntax highlighting from beginning of file.
  autocmd BufEnter * :syntax sync fromstart
  autocmd FileType php let b:AutoPairs = AutoPairsDefine({'<?' : '?>', '<?php': '?>'})
  autocmd FileType ruby let b:AutoPairs = AutoPairsDefine({'begin': 'end//n]'})
  autocmd FileType *html let b:AutoPairs = AutoPairsDefine({'<!--' : '-->', '{%': '%}'})
  " Display help in a vsplit
  autocmd BufWinEnter *.txt if &ft == 'help' | wincmd L | endif
  autocmd BufRead /tmp/mutt-* set tw=72
  autocmd BufNewFile *.vue 0r ~/.config/dotfiles/vim/templates/skeleton.vue
  autocmd BufNewFile ~/doc/wiki/journal/*.md :silent 0r !~/.config/dotfiles/vim/scripts/generate_journal_file.py '%:t'
augroup END

" Detect uv scripts (with uv hashbang) as Python
augroup uv_scripts
  autocmd!
  autocmd BufRead * if getline(1) =~# '^#!.*\<uv\>' | setfiletype python | endif
augroup END

"""""""""""""""""""""""""""""""""""""""""""""
""  Plugin Settings                        ""
"""""""""""""""""""""""""""""""""""""""""""""

call plug#begin()

Plug 'ggandor/leap.nvim'                 " Use s and S to navigate to any two-letter combination
Plug 'jeffkreeftmeijer/vim-numbertoggle' " Turn relative line numbers off in non-focused window
Plug 'jiangmiao/auto-pairs'              " Automatically add closing braces and delete matching ones
Plug 'junegunn/fzf'                      " [Buffers] Jump to the existing window if possible
Plug 'lewis6991/gitsigns.nvim'           " Show git changes/diff indicator in side column
Plug 'neovim/nvim-lspconfig'             " Sane defaults for LSP
Plug 'nvim-mini/mini.nvim'               " Completion from LSP
Plug 'romainl/vim-cool'                  " Removes search highlight when not needed
Plug 'tpope/vim-commentary'              " Toggle comments
Plug 'tpope/vim-fugitive'                " Provides :G[it]
Plug 'tpope/vim-repeat'                  " Repeat plugin commands with .
Plug 'tpope/vim-rsi'                     " Readline in vim
Plug 'tpope/vim-sleuth'                  " Auto-set tabs vs spaces
Plug 'tpope/vim-speeddating'             " Increment/decrement dates
Plug 'tpope/vim-surround'                " Surround text objects
Plug 'tpope/vim-vinegar'                 " Better directory browsing
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'vimwiki/vimwiki'
Plug 'wellle/context.vim'                " Provide context when scrolling <3

call plug#end()

" fzf settings: open at bottom, hide status line
let g:fzf_buffers_jump = 1
let g:fzf_layout = { 'down': '30%' }
autocmd  FileType fzf set laststatus=0 noshowmode noruler
  \| autocmd BufLeave <buffer> set laststatus=2 showmode ruler
nnoremap <C-T> :call fzf#run({ 'sink': 'vsplit', 'down': '30%' })<CR>
nnoremap <C-N> :call fzf#run({ 'sink': 'e', 'down': '30%' })<CR>
" open fzf with the wiki directory
nnoremap <leader>wt :call fzf#run({ 'sink': 'e', 'source': 'fd . /home/rixx/doc/wiki/' })<CR>

" airline settings
let g:airline_powerline_fonts = 1
let g:airline_detect_modified=1
let g:airline_highlighting_cache = 1
let g:airline_detect_paste=1
let g:airline_theme='manuscript'
" Show tabs on top, disable other extensions for performance reasons
" Mouse integration: click to go, middle click to close, right click to delete file
let g:airline_extensions = ['tabline']
let g:airline#extensions#tabline#enabled = 1
" Only show path if the filename isn't unique
let g:airline#extensions#tabline#formatter = 'unique_tail_improved'
let g:airline#extensions#tabline#overflow_marker = '…'
" Show buffer numbers
let g:airline#extensions#tabline#buffer_idx_mode = 1
let g:airline#extensions#tabline#buffer_min_count = 2

" vim-surround settings
" cs"'    change surrounding " to '
" cst"    change surrounding tag to " (matches html)
" ds"     delete surrounding "
" ysiw"   surround word with ". ( for with spaces, ) for without
" yss"    surround line with "
" in visual mode: S to surround a line
" Strike through this line
"" ysss strikes through line, ysiws strikes through word etc, ysiwo bolds word
let g:surround_{char2nr('s')} = "~~\r~~"
let g:surround_{char2nr('b')} = "**\r**"

" vim-autopairs settings: Do **not** jump to the next line when closing a perceived block
let g:AutoPairsMultilineClose = 0

" vim-vinegar bindings
" -       open the directory browser / go up a directory
" I       show header again
" gh      toggle hidden files
" .       add file path to end of cmd
" y.      yank full file path
" ~       go to home
" CTRL-^  go back to file

" vim-fugitive settings
" GBrowse opens the current file on GitHub
" In blame:
" A    resize to end of author column
" C    to end of commit column
" D    to end of date column
" gq   close blame
" o    open patch in hsplit (GVsplit also works)
" -    reblame at commit!!!
"" vim-fugitive is slow with zsh stuff
set shell=bash

" gitsigns settings
" [c           jump to next hunk
" <leader>hu   undo this hunk!!!, works with visual selection
" ic           hunk text object
set timeoutlen=400

" context settings: remove decoration from context
let g:context_highlight_tag = '<hide>'
nnoremap <leader>ct :ContextToggle<CR>

""" vimwiki config
" <leader>ww opens index
" <leader>w<leader>w opens diary
" C-up and C-down navigate between diary entries
" <leader>wy/m opens diary yesterday or tomorrow
"
" = increases header level, - decreases
" [[ and ]] navigate headings. [= and =] to stay on same level
" tab and shift-tab cycle through links
" +, shift-enter, ctrl-enter create new link
" C-space toggles checkbox
" gnt goes to next unfinished task
" gl removes one checkbox, gL removes all in list
" gl- creates a list out of a line with symbol - (* also works, and 1 and a for ordered lists)
" gqq formats a table (you  can also use tab in insert to navigate etc, which will also format the table)
"
" ah is the text object of a header till next header, ih only the contents. aH and iH to include subheadings
let g:vimwiki_list = [{'path': '~/doc/wiki/', 'syntax': 'markdown', 'ext': 'md', 'diary_rel_path': 'journal/', 'diary_index': 'journal', 'diary_header': 'Journal', 'diary_frequency': 'daily', 'auto_diary_index': 1}]
" Use vimwiki globally for markdown
let g:vimwiki_global_ext = 1
" When navigating to a directory, open the index page instead of the directory
let g:vimwiki_dir_link = 'index'
let g:vimwiki_diary_months = {
      \ 1: 'Januar', 2: 'Februar', 3: 'März', 4: 'April', 5: 'Mai', 6: 'Juni',
      \ 7: 'Juli', 8: 'August', 9: 'September', 10: 'Oktober', 11: 'November', 12: 'Dezember'
      \}
let g:vimwiki_links_header = 'Sitemap'
let g:vimwiki_auto_header = 0


lua << EOF
require('mini.snippets').setup({})
require('mini.completion').setup({})
vim.lsp.enable('pyright')

vim.keymap.set('n', 'gd', vim.lsp.buf.definition, {})
vim.keymap.set('n', 'gr', vim.lsp.buf.references, {})
vim.keymap.set('n', 'K', vim.lsp.buf.hover, {})
vim.keymap.set('n', '<leader>rn', vim.lsp.buf.rename, {})

local imap_expr = function(lhs, rhs)
  vim.keymap.set('i', lhs, rhs, { expr = true })
end
imap_expr('<Tab>',   [[pumvisible() ? "\<C-n>" : "\<Tab>"]])
imap_expr('<S-Tab>', [[pumvisible() ? "\<C-p>" : "\<S-Tab>"]])

require('gitsigns').setup({
  signs = {
    add          = { text = '+' },
    change       = { text = '·' },
    delete       = { text = '-' },
    topdelete    = { text = '·' },
    changedelete = { text = '~' },
    untracked    = { text = '?' },
  },
  on_attach = function(bufnr)
    local gitsigns = require('gitsigns')
    local function map(mode, l, r, opts)
      opts = opts or {}
      opts.buffer = bufnr
      vim.keymap.set(mode, l, r, opts)
    end
    map('n', ']c', function() gitsigns.nav_hunk('next') end)
    map('n', '[c', function() gitsigns.nav_hunk('prev') end)
    map('n', '<leader>hs', gitsigns.stage_hunk)
    map('n', '<leader>hr', gitsigns.reset_hunk)
    map('v', '<leader>hs', function() gitsigns.stage_hunk {vim.fn.line('.'), vim.fn.line('v')} end)
    map('v', '<leader>hr', function() gitsigns.reset_hunk {vim.fn.line('.'), vim.fn.line('v')} end)
    map('n', '<leader>hS', gitsigns.stage_buffer)
    map('n', '<leader>hu', gitsigns.undo_stage_hunk)
    map('n', '<leader>hR', gitsigns.reset_buffer)
    map('n', '<leader>hp', gitsigns.preview_hunk)
    map('n', '<leader>hb', function() gitsigns.blame_line{full=true} end)
    map('n', '<leader>tb', gitsigns.toggle_current_line_blame)
    map('n', '<leader>hd', gitsigns.diffthis)
    map('n', '<leader>hD', function() gitsigns.diffthis('~') end)
    map('n', '<leader>td', gitsigns.toggle_deleted)
    map({'o', 'x'}, 'ih', ':<C-U>Gitsigns select_hunk<CR>')
  end
})

require('leap').create_default_mappings()
EOF

" vim:set ft=vim et sw=2:
