#! /bin/zsh -
# speed things up by making mv builtin
zmodload zsh/files

# associative array giving the destination directory for each
# type of file
typeset -A dst=(
  azw3 book
  epub book
  mobi book
  lit book
  tar data
  gz data
  xz data
  zip data
  doc  doc
  docx doc
  ods doc
  odt doc
  pdf doc
  pptx doc
  ppt doc
  rtf doc
  xlsx doc
  gif  img
  ico  img
  jpg  img
  jpeg img
  png img
  svg img
  webp img
  csv txt
  css txt
  eml txt
  html txt
  js txt
  json txt
  log txt
  txt txt
  py txt
  sh txt
  zsh txt
  mp3 sound
  ogg sound
  flv vid
  mp4 vid
  mkv vid
  mov vid
)

# default for files with extensions not covered by $dst above or
# files without extension
default=misc

mkdir -p $dst $default || exit

for f (*(N.)) mv -i -- $f ${dst[$f:e:l]-$default}/
