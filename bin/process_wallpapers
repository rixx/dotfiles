#!/bin/zsh
# Iterate through wallpaper directory, resizing and adding the input box in the right place

set -e

wallpaper_folder=~/lib/pics/wallpapers

# process a single folder, this is where the actual work is done
# needs to be a function so that the base_folder variable is in scope
function process_folder {
	base_folder=$1
	processed_folder="$base_folder/processed"

	if [ ! -d $processed_folder ]; then
		mkdir -p $processed_folder;
	fi;

	res=$(xdpyinfo | grep dimensions | sed -r 's/^[^0-9]*([0-9]+x[0-9]+).*$/\1/')
	for f in $base_folder/unprocessed/*; do;
		if [[ $(file -b $f) =~ 'image data' ]]; then
			target="$processed_folder/${f:t}"
			if [[ ! -a "$target" ]]; then;
				echo $f
				tmpfile="$processed_folder/tmp.${f:t}"
				convert "$f" -resize "$res""^" -gravity center -extent "$res" "$tmpfile"
				convert "$tmpfile" -draw "fill #00000066 $rectangles" "$target"
				rm "$tmpfile"
			fi;
		fi;
	done;
}

rectangles="rectangle 25,1050 325,970"

# iterate over the different resolution directories in the wallpaper folder

for base_folder in $wallpaper_folder/*; do
	if [ -d $base_folder ]; then
		# process the folder
		process_folder $base_folder
	fi;
done;
